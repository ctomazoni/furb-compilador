#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
mai : [A-Z]
min : [a-z]
dig : [0-9]
letra : {mai}|{min}
coment_linha: !![^\n]*
coment_bloco: $[^$]*$



#Tokens
//ignorar caracteres de formatação
:[\t\n\s]
: {coment_linha}
: {coment_bloco}

identificador:{letra}(_?({letra}|{dig})+)*_? 
const_inteira: 0|[1-9]({dig})*
const_real: (0|[1-9]({dig})*)","({dig})+
const_caract: \"(([^\n\\\"])*|"\\"|"\"\")*\"

//palavras reservadas
ENDIF = identificador : "endIf"
BOOL = identificador : "bool"
CONSTS = identificador : "consts"
DEF = identificador : "def"
END = identificador : "end"
EXECUTE = identificador : "execute"
FALSE = identificador : "false"
FLOAT = identificador : "float"
GET = identificador : "get"
IFFALSE = identificador : "ifFalse"
IFTRUE = identificador : "ifTrue"
INPUT = identificador : "input"
INT = identificador : "int"
PRINT = identificador : "print"
PRINTLN = identificador : "println"
SET = identificador : "set"
STR = identificador : "str"
TRUE = identificador : "true"
TYPES = identificador : "types"
VAR = identificador : "var"
WHILEFALSE = identificador : "whileFalse"
WHILETRUE = identificador : "whileTrue"

//simbolos especiais
":"
"("
")"
"="
":="
","
"&&"
"||"
"!"
"!="
"<"
">="
">"
"<="
"+"
"-"
"*"
"/"







#NonTerminals
<programa>
<definicao_tipos>
<def_tipo>
<def_tipo_1>
<eh_tipo>
<simples>
<composto>
<lista_campos>
<lista_campos_1>
<campo>
<definicao_constantes>
<constantes>
<constantes_1>
<lista_identificadores>
<lista_identificadores_1>
<valor>
<declaracao_variaveis>
<variaveis>
<variaveis_1>
<tipo>
<lista_comandos>
<lista_comandos_1>
<comando>
<comando_1>
<comando_2>
<comando_3>
<comando_entrada>
<comando_saida>
<lista_expressao>
<lista_expressao_1>


<expressão>
<expressão1>
<elemento>
<relacional>
<relacional_1>
<operador_relacional>
<aritmética>
<aritimética_1>
<termo>
<termo_1>
<fator>
<fator_1>
#Grammar

<programa> ::= DEF <definicao_tipos> <definicao_constantes> <declaracao_variaveis> EXECUTE <lista_comandos>;

<definicao_tipos> ::= î | TYPES <def_tipo>;
<def_tipo> ::= identificador ":" <eh_tipo> <def_tipo_1> ;
<def_tipo_1> ::= î | <def_tipo>;

<eh_tipo> ::= <simples> | <composto>;
<simples> ::= INT | FLOAT | STR | BOOL;
<composto> ::= "("<lista_campos>")";
<lista_campos> ::= <campo><lista_campos_1>;
<lista_campos_1> ::= î | <lista_campos>;

<campo> ::= identificador ":" <simples>;

<definicao_constantes> ::= î | CONSTS <constantes>;

<constantes> ::= <lista_identificadores> "=" <valor><constantes_1>;
<constantes_1> ::= î | <constantes>;

<lista_identificadores> ::= identificador <lista_identificadores_1>;
<lista_identificadores_1> ::= î | ","<lista_identificadores>;

<valor> ::= const_inteira | const_real | const_caract | TRUE | FALSE;

<declaracao_variaveis> ::= î | VAR <variaveis>;

<variaveis> ::= <lista_identificadores> ":" <tipo><variaveis_1>;
<variaveis_1> ::= î | <variaveis>;

<tipo> ::= <simples> | identificador;

<lista_comandos> ::= <comando><lista_comandos_1>;
<lista_comandos_1> ::= î | <lista_comandos>;

<comando> ::= identificador <comando_1> | <comando_entrada> | <comando_saida> | "(" <expressão> ")" <comando_2>;
<comando_1> ::= ":=" <expressão> | SET "(" identificador"," <expressão> ")";
<comando_2> ::=  IFTRUE ":" <lista_comandos><comando_3> | 
	         IFFALSE ":" <lista_comandos> END | 
		WHILETRUE":" <lista_comandos> END |
		WHILEFALSE":" <lista_comandos> END;

<comando_3> ::=  END | IFFALSE ":" <lista_comandos> END;


<comando_entrada> ::= INPUT "(" <lista_identificadores> ")";
<comando_saida> ::= PRINT "(" <lista_expressao> ")" | PRINTLN "(" <lista_expressao> ")";
<lista_expressao> ::= <expressão> <lista_expressao_1>;
<lista_expressao_1> ::= î | ","<lista_expressao>;


<expressão> ::= <elemento><expressão1>;
<expressão1> ::= "&&" <elemento><expressão1> | "||" <elemento><expressão1> | î;
<elemento> ::= <relacional> | TRUE | FALSE | "!" <elemento>;
<relacional> ::= <aritmética> <relacional_1>;
<relacional_1> ::= î | <operador_relacional> <aritmética>;

<operador_relacional> ::= "=" | "!=" | "<" | "<=" | ">" | ">=";

<aritmética> ::= <termo><aritimética_1>;
<aritimética_1> ::= î | "+" <termo> <aritimética_1> | "-" <termo> <aritimética_1>;

<termo> ::= <fator><termo_1>;
<termo_1> ::=  î | "*" <fator><termo_1> | "/" <fator><termo_1>;

<fator> ::= identificador <fator_1> | const_inteira | const_real | const_caract | "(" <expressão> ")" |
 	    "+" <fator> | "-" <fator>;

<fator_1> ::=  î | GET "(" identificador ")";
